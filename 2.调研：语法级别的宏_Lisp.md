# Lisp 宏

Lisp的宏与C的宏有极大的差别。Lisp的宏是一种**变形(transform) Lisp代码的方法**。

在宏展开阶段，Lisp表达式将被传递给宏函数（macro function）。这个宏函数可以在宏扩展时进行任意的计算。

该宏函数调用的**结果**必须同样也是**Lisp代码** ——也就是解释器或者编译器之后会看到并执行或者编译的。

非常重要的一点是：Lisp的宏函数使用的即使Lisp语言本身，而不像C语言的宏，宏使用的语法和C语言本身的语法并不同，并且C语言的宏对C语言本身一无所知，它几乎只是一种文本操作机制。

[段落参考](http://wiki.c2.com/?LispMacro)

## 引入

#### 简单的例子：

定义宏：

`(defmacro eight () (+ 3 5))`

使用宏：

`(eight)`

这与直接使用数字 8 是等价的，因为这一步计算已经在**编译时**被完成了。

#### 使用单引号的例子

定义宏：

`(defmacro eight () '(+ 3 5))`

使用宏：

`(eight)`

此时，我们对宏函数内的表达式 `(+ 3 5)`  使用**单引号** (')括起。和上一个例子相比，虽然相似，但是在被展开的时候，他会返回 `(+ 3 5)` 而不会在编译时计算结果。

#### 关于 Lisp 中的 quote

上例的单引号是Lisp中的运算符quote的一种简写法，具体而言， `(quote symbol)` 和 `'symbol` 等价。

Lisp中，quote 对一个表达式的作用是使它不被求值，而是直接将表达式传递给函数。它改变了Lisp默认的求值规则。

比如 `(print '(+ 3 4))` 打印 `(+ 3 4)` ， 而 `(print (+ 3 4))` 打印 `7`。

对于我们上面的带有单引号的宏的例子，由于宏函数中的表达式使用了一个单引号，所以宏函数调用产生的结果是**Lisp代码** `(+ 3 5)` ，当这段代码放入宏展开的位置时，这个单引号不在宏函数的返回值（即代码）中。

调用宏：

```lisp
(defmacro m1 ()    (+ 3 5))
(defmacro m2 ()   '(+ 3 5))
(defmacro m3 ()  ''(+ 3 5))
(defmacro m4 () '''(+ 3 5))
```

使用宏：

```lisp
(print(m1))
(print(m2))
(print(m3))
(print(m4))
```

展开为：

```lisp
(print 8)
(print (+ 3 5))
(print '(+ 3 5))
(print ''(+ 3 5))
```

运行结果：

```
8 
8 
(+ 3 5) 
'(+ 3 5) 
```

宏函数 `m3` 使用了两个单引号，故宏展开后在 `print` 函数的参数表达式中还留有一层 quote，所以，答应出了表达式本身而非值。类似的， `m4` 展开后有两个单引号，故打印出的表达式还留有一个单引号。

[段落参考1](http://wiki.c2.com/?LispMacro)
[段落参考2](https://stackoverflow.com/questions/134887/when-to-use-or-quote-in-lisp)

## Lisp宏作为代码变形方式

如前文所述，Lisp宏可以作为一种变形代码的方式。

#### 举例：Python 中的 list comprehensions（列表解析式）

List comprehensions（列表解析式）是Python中的一个语法糖。

`divisibleByTwo = [x for x in range(10) if x % 2 == 0]` 产生了一个包含0~9中可被2整除的所有数字的 list。

在Python的早期（比如Python 1.5的时期），并没有引入这样的语法糖，要实现上面的功能，需要使用以下的代码：

```python
divisibleByTwo = []
for x in range( 10 ):
   if x % 2 == 0:
      divisibleByTwo.append( x )
```

在Lisp中，我们可以使用以下的代码（不使用Lisp宏）来达到相似的功能。实现了range功能，并使用了类似无list comprehensions语法糖的Python代码的结构。

```lisp
;; the following two functions just make equivalent of Python's range function
;; you can safely ignore them unless you are running this code
(defun range-helper (x)
  (if (= x 0)
      (list x)
      (cons x (range-helper (- x 1)))))

(defun range (x)
  (reverse (range-helper (- x 1))))

;; equivalent to the python example:
;; define a variable
(defvar divisibleByTwo nil)

;; loop from 0 upto and including 9
(loop for x in (range 10)
   ;; test for divisibility by two
   if (= (mod x 2) 0) 
   ;; append to the list
   do (setq divisibleByTwo (append divisibleByTwo (list x))))
```

而使用Lisp宏，则可以在以上函数的基础上，添加类似于list comprehensions的语法结构来实现类似功能。

宏的定义如下：

```lisp
(defmacro lcomp (expression for var in list conditional conditional-test)
  ;; create a unique variable name for the result
  (let ((result (gensym)))
    ;; the arguments are really code so we can substitute them 
    ;; store nil in the unique variable name generated above
    `(let ((,result nil))
       ;; var is a variable name
       ;; list is the list literal we are suppose to iterate over
       (loop for ,var in ,list
            ;; conditional is if or unless
            ;; conditional-test is (= (mod x 2) 0) in our examples
            ,conditional ,conditional-test
            ;; and this is the action from the earlier lisp example
            ;; result = result + [x] in python
            do (setq ,result (append ,result (list ,expression))))
           ;; return the result 
       ,result)))
```

而使用 `(lcomp x for x in (range 10) if (= (mod x 2) 0))` 即可获得0~9之前的可被2整除的数字。

[段落引用](https://stackoverflow.com/questions/267862/what-makes-lisp-macros-so-special)

